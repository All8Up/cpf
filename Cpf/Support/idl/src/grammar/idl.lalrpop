use ast::*;
use crc::*;

grammar;

Identifier: String =
{
	ID => <>.to_string()
};

BinaryChar: i32 =
{
	"0" => 0,
	"1" => 1
};

QualifiedIdentifier: String =
{
	Identifier,
	<l:QualifiedIdentifier> ":" ":" <r:Identifier> => {l + "::" + &r}
};

//
Access: DirectionAnnotation =
{
	In => DirectionAnnotation::In,
	Out => DirectionAnnotation::Out
};

PrimitiveIntegralType: DataType =
{
	U8 => DataType::U8,
	I8 => DataType::I8,
	U16 => DataType::U16,
	I16 => DataType::I16,
	U32 => DataType::U32,
	I32 => DataType::I32,
	U64 => DataType::U64,
	I64 => DataType::I64,
	SizeT => DataType::SizeT
};

PrimitiveFloatType: DataType =
{
	F32 => DataType::F32,
	F64 => DataType::F64
};

PrimitiveCharType: DataType =
{
	Char => DataType::Char,
	WChar => DataType::WChar
};

AnyPrimitiveType: DataType =
{
	PrimitiveIntegralType,
	PrimitiveFloatType,
	PrimitiveCharType
};

CompoundType: DataType =
{
	Result => DataType::Result,
	Identifier => DataType::NamedType {name: <>}
};

Types: DataType =
{
	AnyPrimitiveType,
	CompoundType,
	Void => DataType::Void
};

TypeQualifier: TypeQualifier =
{
	Const => TypeQualifier::Const
};

// Type handling needs:
//	[in] u8 c
//	[in, out] u8* n
//	[in] const u16* p
//	[out] iTest** iface

TypeAnnotation: Annotation =
{
	"[" <l:Access> <r:("," <Access>)?> "]" =>
		Annotation {left: Some(l), right: r}
};

PointerDef: u32 =
{
	"*" => 1,
	<p:PointerDef> "*" => p+1
};

ModifiedType: ModifiedType =
{
	<q:(TypeQualifier)?> <t:Types> =>
		ModifiedType {qualifier: q, data_type: t, indirection_count: 0},
	<q:(TypeQualifier)?> <t:Types> <p:PointerDef> =>
		ModifiedType {qualifier: q, data_type: t, indirection_count: p}
};

TypeDecl: TypeDecl =
{
	<a:(TypeAnnotation)?> <t:ModifiedType> =>
		TypeDecl {annotation: a, data_type: t}
};

// Constant initializers.
ConstDecl: NodeRef<Data> =
{
	Success <n:Identifier> "=" "{" <ss:DQuotedString> "," <v:DQuotedString> "}" SemiColon =>
		NodeRef::new(Data::ResultConst {name: n, is_error: false, sub_system: crc16(&ss[1..ss.len()-1]), code: crc15(&(v[1..v.len()-1]))}),
	Failure <n:Identifier> "=" "{" <ss:DQuotedString> "," <v:DQuotedString> "}" SemiColon =>
		NodeRef::new(Data::ResultConst {name: n, is_error: true, sub_system: crc16(&ss[1..ss.len()-1]), code: crc15(&(v[1..v.len()-1]))})
};

// ------
ParamDecl: NodeRef<Data> =
{
	<t:TypeDecl> <n:(Identifier)?> =>
		NodeRef::new(Data::ParamDecl {name: n, type_decl: t})
};

ParamList: NodeRef<Data> =
{
	<p:ParamDecl> => p,
	<l:ParamList> "," <r:ParamDecl> =>
		{
			let mut target = l.clone();
			loop
			{
				match target.next_sibling()
				{
					None => {target.insert_after(r); break},
					Some(next) => target = next
				}
			}
			l
		}
};

MemberSignature: NodeRef<Data> =
{
	<r:ModifiedType> <n:Identifier> "(" ")" SemiColon =>
		NodeRef::new(Data::MemberSignature {name: n.to_string(), result_type: r}),
	<r:ModifiedType> <n:Identifier> "(" <p:ParamList> ")" SemiColon =>
	{
		let node = NodeRef::new(Data::MemberSignature {name: n.to_string(), result_type: r});
		for nn in p.following_siblings()
		{
			node.append(nn);
		}
		node
	}
};

// ------

ClassIDStmt: NodeRef<Data> =
{
	ClassID <n:Identifier> "(" <v:SQuotedString> ")" SemiColon =>
		NodeRef::new(Data::ClassID {name: n, cid: v.to_string()})
};

InterfaceIDStmt: NodeRef<Data> =
{
	InterfaceID <n:Identifier> "(" <v:SQuotedString> ")" SemiColon =>
		NodeRef::new(Data::InterfaceID {name: n, iid: v.to_string()})
};

ImportStmt: NodeRef<Data> =
{
	Import <n:DQuotedString> =>
		NodeRef::new(Data::Import {name: (n[1..n.len()-1]).to_string()})
};

InterfaceStmts: NodeRef<Data> =
{
	InterfaceIDStmt,
	MemberSignature,
	Comment => NodeRef::new(Data::Comment {content: <>.to_string()})
};

InterfaceBlock: NodeRef<Data> =
{
	<ns:InterfaceStmts> =>
	{
		ns
	},
	<l:InterfaceBlock> <r:InterfaceStmts> =>
	{
		let mut target = l.clone();
		loop
		{
			match target.next_sibling()
			{
				None => {target.insert_after(r); break},
				Some(next) => target = next
			}
		}
		l
	}
};

InterfaceStmt: NodeRef<Data> =
{
	Interface <n:Identifier> SemiColon => NodeRef::new(Data::FwdInterface {name: n}),
	Interface <n:Identifier> <q:(":" <QualifiedIdentifier>)?> "{" <b:InterfaceBlock> "}" =>
	{
		let node = NodeRef::new(Data::Interface {name: n, base: q});
		for nn in b.following_siblings()
		{
			node.append(nn);
		}
		node
	}
};

StructStmt: NodeRef<Data> =
{
	Struct <n:Identifier> SemiColon => NodeRef::new(Data::FwdStruct {name: n})
};

// --- Global statements ---

GlobalScopeStmt: NodeRef<Data> =
{
	ImportStmt,
	InterfaceStmt,
	StructStmt,
	NamespaceStmt,
	ClassIDStmt,
	Comment => NodeRef::new(Data::Comment {content: <>.to_string()})
};

NamespaceScopeStmt: NodeRef<Data> =
{
	NamespaceStmt,
	InterfaceStmt,
	StructStmt,
	ClassIDStmt,
	ConstDecl,
	Comment => NodeRef::new(Data::Comment {content: <>.to_string()})
};

NamespaceStmt: NodeRef<Data> =
{
	Namespace <n:Identifier> "{" <i:(NamespaceBlock)?> "}" =>
	{
		let nn = NodeRef::new(Data::Namespace {name: n});
		match i
		{
			None => (),
			Some(block) =>
			{
				// TODO: append clears the prev/next sibling before insertion, I want to just 'set' the siblings to be block.
				for node in block.following_siblings()
				{
					nn.append(node);
				}
			}
		}
		nn
	}
};

NamespaceBlock: NodeRef<Data> =
{
	<ns:NamespaceScopeStmt> =>
	{
		ns
	},
	<l:NamespaceBlock> <r:NamespaceScopeStmt> =>
	{
		let mut target = l.clone();
		loop
		{
			match target.next_sibling()
			{
				None => {target.insert_after(r); break},
				Some(next) => target = next
			}
		}
		l
	}
};

// Returns a block of all global statments.
GlobalBlock: NodeRef<Data> =
{
	<s:GlobalScopeStmt> =>
	{
		s
	},
	<l:GlobalBlock> <r:GlobalScopeStmt> =>
	{
		// TODO: Need an 'append_sibling' as this insert_after is not what we really want.
		// TODO: In general the entire tree interface is non-intuitive and needs a refactor.
		let mut target = l.clone();
		loop
		{
			match target.next_sibling()
			{
				None => {target.insert_after(r); break},
				Some(next) => target = next
			}
		}
		l
	}
};

pub IDL: NodeRef<Data> =
{
	GlobalBlock
};


// ----- Lexer precedence.
match {
	"success" => Success,
	"failure" => Failure,
	"class_id" => ClassID,
	"interface_id" => InterfaceID,
	"import" => Import,
	"namespace" => Namespace,
	"interface" => Interface,
	"struct" => Struct,
	"u8" => U8,
	"i8" => I8,
	"u16" => U16,
	"i16" => I16,
	"u32" => U32,
	"i32" => I32,
	"u64" => U64,
	"i64" => I64,
	"f32" => F32,
	"f64" => F64,
	"char" => Char,
	"wchar" => WChar,
	"void" => Void,
	"size_t" => SizeT,
	"result" => Result,
	"const" => Const,
	"in" => In,
	"out" => Out,
	"true" => True,
	"false" => False,
	";" => SemiColon,
	r"[_a-zA-Z][_a-zA-Z0-9]*" => ID
} else {
	"[0-9]" => Digit
} else {
	r#"'[^']*'"# => SQuotedString,
	r#""[^"]*""# => DQuotedString,
	r"/\*[.]*\*/" => Comment,
	_
}
